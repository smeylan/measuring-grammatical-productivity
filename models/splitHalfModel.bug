model {

  /* indexing is complicated for nouns
  part1: only the child produce the noun in the sample (very few cases!)
  part2: child and caregiver both produce the noun
  part3: only the caregiver produces the noun
  part4: caregivers don't produce the noun in this sample, but imputing a count */

  /* in the case of the split-half model, two phases are labeled */

  /* prior over mean and proportion for beta (determines cross-noun distribution of determiner preference) */
  lambda <- 0.0005 
  sigma <- 10000 

  /* FIRST PHASE */
  mu0.adult1 ~ dbeta(1,1)
  nu.adult1 ~ dgamma(1,lambda) 

  mu0.child1 ~ dbeta(1,1)
  nu.child1 ~ dgamma(1,lambda) 
  
  a.adult1 <- mu0.adult1*nu.adult1
  b.adult1 <- (1-mu0.adult1)*nu.adult1
  a.child1 <- mu0.child1*nu.child1
  b.child1 <- (1-mu0.child1)*nu.child1
  
  for(i in (phase1_endPart1+1):phase1_endPart3) {
    mu.parent1[i] ~ dbeta(a.adult1, b.adult1) T(0.001,0.999) 
    r.parent1[i] ~ dbin(mu.parent1[i],n.parent1[i])
  }

  /* SECOND PHASE */
  mu0.adult2 ~ dbeta(1,1)
  nu.adult2 ~ dgamma(1,lambda) 
  mu0.child2 ~ dbeta(1,1)
  nu.child2 ~ dgamma(1,lambda)
  /*
  nu.change ~ dnorm(0,1/sigma^2)
  nu.child2 <- ifelse(nu.child1+nu.change < 0, 0, nu.child1+nu.change)
  */

  a.adult2 <- mu0.adult2*nu.adult2
  b.adult2 <- (1-mu0.adult2)*nu.adult2
  a.child2 <- mu0.child2*nu.child2
  b.child2 <- (1-mu0.child2)*nu.child2
  for(i in (phase2_endPart1+1):phase2_endPart3) {
    mu.parent2[i] ~ dbeta(a.adult2, b.adult2) T(0.001,0.999)
    r.parent2[i] ~ dbin(mu.parent2[i], n.parent2[i])
  }
  
  /* Imputation step: externally set a total number Q of det+n utterances experienced by the child in its lifetime, and a word-frequency distribution wordfreqs */

  /* FIRST PHASE */
  for(i in 1:phase1_endPart4) {
    MU.parent1[i] ~ dbeta(a.adult1 + r.parent1[i], b.adult1 + n.parent1[i] - r.parent1[i])T(0.001,0.999)
    R.parent1[i] ~ dbin(MU.parent1[i], N.parent1[i]) /* N.parent1 is the number to be imputed  in the first phase*/
  }

  /* SECOND PHASE */
  for(j in 1:phase2_endPart4) {
    MU.parent2[j] ~ dbeta(a.adult2 + r.parent2[j], b.adult2 + n.parent2[j] - r.parent2[j])T(0.001,0.999)
    R.parent2[j] ~ dbin(MU.parent2[j], N.parent2[j]) /* N.parent2 is the number to be imputed in the second phase  */
  }

  /* downweighting factor on observed utterances (memory limitations of child & such).  Set to 1 for perfect memory */
  eta1 ~ dunif(0,1)
  eta2 ~ dunif(0,1)    

  /* OBSERVED CHILD DATA */
  
  /* FIRST PHASE */  
  for(k in 1:phase1_endPart2) {
    /* pseudocounts */
    pseudo.r1[k] <- eta1 * (r.parent1[k] + R.parent1[k])
    pseudo.f1[k] <- eta1 * (n.parent1[k] - r.parent1[k] + N.parent1[k] - R.parent1[k])

    /* infer values of mu and nu from observed child production data */
    mu.child1[k] ~ dbeta(a.child1+pseudo.r1[k],b.child1+pseudo.f1[k]) T(0.001,0.999)

    r.child1[k] ~ dbin(mu.child1[k], n.child1[k]) 
    
    /* simulated child production data: encounter length(N.new) noun types with n.child[k] observations each */
    mu.child1.simulated[k] ~ dbeta(a.child1+pseudo.r1[k], b.child1+pseudo.f1[k]) T(0.001,0.999)

    r.child.simulated1[k] ~ dbin(mu.child1.simulated[k],n.child1[k]) 
  }

  /* SECOND PHASE */  
  for(m in 1:phase2_endPart2) {
    /* pseudocounts */
    pseudo.r2[m] <- eta2 * (r.parent2[m] + R.parent2[m])
    pseudo.f2[m] <- eta2 * (n.parent2[m] - r.parent2[m] + N.parent2[m] - R.parent2[m])

    /* infer values of mu and nu from observed child production data */
    mu.child2[m] ~ dbeta(a.child2+pseudo.r2[m], b.child2+pseudo.f2[m]) T(0.001,0.999)
    r.child2[m] ~ dbin(mu.child2[m], n.child2[m]) 
    
    /* simulated child production data: encounter length(N.new) noun types with n.child[m] observations each */
    mu.child2.simulated[m] ~ dbeta(a.child2+pseudo.r2[m], b.child2+pseudo.f2[m]) T(0.001,0.999)
    r.child.simulated2[m] ~ dbin(mu.child2.simulated[m], n.child2[m]) 
    
  }
}